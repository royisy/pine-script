//@version=5
indicator("Volume Density Candle", "VDC", overlay=true)

AVG_TRANS = 50
avgLength = input.int(20, "Average length", minval=1, maxval=200)
maxDeviationRate = input.float(1, "Max deviation rate", minval=0.1, maxval=2, step=0.1)
excludeFirst = input.bool(false, "Exclude the first candle of the intraday session from the average")
excludeLast = input.bool(true, "Exclude the last candle of the intraday session from the average")
maxTrans = input.int(80, "Max transparency", minval=70, maxval=100)
upColor = input.color(color.teal, "Up color")
downColor = input.color(color.red, "Down color")

float trans = 0
if ta.tr(true) > 0 and not na(volume) and volume > 0
    float sum = 0
    count = 0
    for i = 0 to avgLength - 1
        if timeframe.isintraday and i > 0
            if excludeFirst
                if not na(time_tradingday[i + 1])
                    if time_tradingday[i] != time_tradingday[i + 1]
                        continue
            if excludeLast
                if time_tradingday[i] != time_tradingday[i - 1]
                    continue
        if ta.tr(true)[i] > 0
            volumeDensity = volume[i] / ta.tr(true)[i]
            sum := sum + volumeDensity
            count += 1
    float avgVolumeDensity = 0
    if count > 0
        avgVolumeDensity := sum / count
    currentVolumeDensity = volume / ta.tr(true)
    float deviationRate = maxDeviationRate
    if avgVolumeDensity > 0
        deviationRate := (currentVolumeDensity - avgVolumeDensity) / avgVolumeDensity
        deviationRate := deviationRate > maxDeviationRate ? maxDeviationRate : deviationRate
        deviationRate := deviationRate < -maxDeviationRate ? -maxDeviationRate : deviationRate
    float diffTrans = AVG_TRANS * deviationRate / maxDeviationRate
    trans := AVG_TRANS - diffTrans
    trans := trans > maxTrans ? maxTrans : trans
upColor := color.new(upColor, trans)
downColor := color.new(downColor, trans)
color = close >= open ? upColor : downColor

plotcandle(open, high, low, close, color=color, wickcolor=color, bordercolor=color)
